---
page_title: "netbox_ip_address Resource - terraform-provider-netbox"
subcategory: ""
description: |-
  Manages an IP address in Netbox.
---

# netbox_ip_address (Resource)

Manages an IP address in Netbox.

## Example Usage

```terraform
resource "netbox_ip_address" "nat_inside" {
  address = "10.0.0.254/24"
}

resource "netbox_ip_address" "test_v4" {
  address     = "10.0.0.1/24"
  status      = "active"
  nat_inside  = netbox_ip_address.nat_inside.id
  dns_name    = "test.example.com"
  description = "Primary web server IP"

  # Partial custom fields management
  # Only specified custom fields are managed, others preserved
  custom_fields = [
    {
      name  = "owner_team"
      value = "web-ops"
    },
    {
      name  = "purpose"
      value = "load-balancer-vip"
    },
    {
      name  = "monitoring_enabled"
      value = "true"
    }
  ]

  tags = [
    "production",
    "web-tier"
  ]
}

resource "netbox_ip_address" "test_v6" {
  address     = "2001:db8::1/64"
  status      = "active"
  description = "IPv6 primary address"

  # Partial custom fields management
  custom_fields = [
    {
      name  = "ipv6_deployment_phase"
      value = "production"
    }
  ]

  tags = [
    "ipv6",
    "production"
  ]
}

resource "netbox_vrf" "test" {
  name = "Test VRF"
  rd   = "65000:1"
}

resource "netbox_ip_address" "test_vrf" {
  address     = "192.168.1.1/24"
  vrf         = netbox_vrf.test.name
  status      = "active"
  description = "VRF gateway address"

  # Partial custom fields management
  custom_fields = [
    {
      name  = "gateway_role"
      value = "default"
    },
    {
      name  = "vrf_priority"
      value = "high"
    }
  ]

  tags = [
    "gateway",
    "vrf-test"
  ]
}

# IP address assigned to a device interface
resource "netbox_site" "ip_example" {
  name   = "IP Example Site"
  slug   = "ip-example-site"
  status = "active"
}

resource "netbox_manufacturer" "ip_example" {
  name = "IP Example Manufacturer"
  slug = "ip-example-manufacturer"
}

resource "netbox_device_type" "ip_example" {
  model        = "IP Example Device Type"
  slug         = "ip-example-device-type"
  manufacturer = netbox_manufacturer.ip_example.id
  u_height     = 1
}

resource "netbox_device_role" "ip_example" {
  name  = "IP Example Role"
  slug  = "ip-example-role"
  color = "ff0000"
}

resource "netbox_device" "ip_example" {
  name        = "ip-example-device"
  device_type = netbox_device_type.ip_example.id
  role        = netbox_device_role.ip_example.id
  site        = netbox_site.ip_example.id
  status      = "active"
}

resource "netbox_interface" "ip_example" {
  device = netbox_device.ip_example.name
  name   = "eth0"
  type   = "1000base-t"
}

resource "netbox_ip_address" "assigned_device" {
  address              = "10.10.0.10/24"
  status               = "active"
  assigned_object_type = "dcim.interface"
  assigned_object_id   = netbox_interface.ip_example.id
  description          = "Assigned to device interface"
}

# IP address assigned to a VM interface
resource "netbox_cluster_type" "ip_vm" {
  name = "IP VM Cluster Type"
  slug = "ip-vm-cluster-type"
}

resource "netbox_cluster" "ip_vm" {
  name = "IP VM Cluster"
  type = netbox_cluster_type.ip_vm.id
}

resource "netbox_virtual_machine" "ip_vm" {
  name    = "ip-example-vm"
  cluster = netbox_cluster.ip_vm.name
  status  = "active"
}

resource "netbox_vm_interface" "ip_vm" {
  name            = "eth0"
  virtual_machine = netbox_virtual_machine.ip_vm.name
}

resource "netbox_ip_address" "assigned_vm" {
  address              = "192.0.2.10/24"
  status               = "active"
  assigned_object_type = "virtualization.vminterface"
  assigned_object_id   = netbox_vm_interface.ip_vm.id
  description          = "Assigned to VM interface"
}

# Optional: seed owned custom fields during import
import {
  to = netbox_ip_address.test_v4
  id = "123"

  identity = {
    custom_fields = [
      "owner_team:text",
      "purpose:text",
      "monitoring_enabled:boolean",
    ]
  }
}

# Optional: seed owned custom fields during import
import {
  to = netbox_ip_address.test_v6
  id = "124"

  identity = {
    custom_fields = [
      "ipv6_deployment_phase:text",
    ]
  }
}

# Optional: seed owned custom fields during import
import {
  to = netbox_ip_address.test_vrf
  id = "125"

  identity = {
    custom_fields = [
      "gateway_role:text",
      "vrf_priority:text",
    ]
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `address` (String) The IP address with prefix length (e.g., 192.168.1.1/24).

### Optional

- `assigned_object_id` (Number) The ID of the assigned object (interface or VM interface).
- `assigned_object_type` (String) The content type of the assigned object (e.g., `dcim.interface`, `virtualization.vminterface`).
- `comments` (String) Additional comments or notes about the IP address. Supports Markdown formatting.
- `custom_fields` (Attributes Set) Custom fields assigned to this resource. Custom fields must be defined in Netbox before use. (see [below for nested schema](#nestedatt--custom_fields))
- `description` (String) Description of the IP address.
- `dns_name` (String) Hostname or FQDN (not case-sensitive).
- `nat_inside` (String) ID or address of the inside IP address for NAT (the IP for which this address is the outside IP).
- `role` (String) The role of the IP address. Valid values are: `loopback`, `secondary`, `anycast`, `vip`, `vrrp`, `hsrp`, `glbp`, `carp`.
- `status` (String) The status of the IP address. Valid values are: `active`, `reserved`, `deprecated`, `dhcp`, `slaac`. Defaults to `active`.
- `tags` (Set of String) Tags assigned to this resource. Tags must already exist in Netbox.
- `tenant` (String) ID or slug of the tenant this IP address is assigned to.
- `vrf` (String) ID or name of the VRF this IP address is assigned to.

### Read-Only

- `id` (String) The unique numeric ID of the IP address.

<a id="nestedatt--custom_fields"></a>
### Nested Schema for `custom_fields`

Required:

- `name` (String) Name of the custom field.
- `type` (String) Type of the custom field (text, longtext, integer, boolean, date, url, json, select, multiselect, object, multiobject).
- `value` (String) Value of the custom field.

## Import

Import is supported using the following syntax:

```shell
# IP Addresses can be imported by ID
terraform import netbox_ip_address.test_v4 123
```
